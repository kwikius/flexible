

%option noyywrap
%x MODE_ML_COMMENT
%x MODE_SL_COMMENT
%x MODE_STRING
%x MODE_CHARSEQ

%{

  //####################

#include <cassert>
#include <cstdlib>

#define YY_NO_UNISTD_H

#include "token_id.hpp"
#include "src_lexer.hpp"

#define YY_DECL int quan_lexer::src_lexer::yylex(token_type & tok)

#define yyterminate() \
   tok.filepos.position = m_position;\
   return tok.m_token_id = quan_lexer::eof;

#undef yywrap
#define yywrap() 1

#undef yyFlexLexer

using std::cin;
using std::cout;
using std::cerr;

   //#############################
   // derive own class from
   // yyFlexLexer
   // the yyFlexLexer must be defined
   // for linkage purposes

int yyFlexLexer::yylex(){assert(false); return 0;}

%} /*############################## */

BLANK             [ \t]
ALPHA             [A-Za-z]
DIGIT             [0-9]
HEXDIGIT          [0-9A-Fa-f]
BINARYDIGIT       [01]
OPERATOR          [!&+*=|^?.$]
PUNCT             [\(\)\{\}\[\]\;\,\.\"]
NOTNL             .
IDCHAR            {ALPHA}|"_"
UNIXNEWL          [\n]
MACNEWL           [\r]
ESCCHAR           [\'\"\\abfnrtv]
NEWL              {UNIXNEWL}|({MACNEWL}{UNIXNEWL}?)
NONESCAPECHAR     {ALPHA}|{DIGIT}|{OPERATOR}|{PUNCT}|"_"
HEXESCAPECHAR     "x\("{HEXDIGIT}+
DECESCAPECHAR     "d\("{DIGIT}+
BINARYESCAPECHAR  "B\("{BINARYDIGIT}+
NUMERICESCAPE     "\\"({HEXESCAPECHAR}|{DECESCAPECHAR}|{BINARYESCAPECHAR})"\)"
CHARESCAPE        "\\"{ESCCHAR}
 /* any of 256 source characters */
ANY               ({NOTNL}|{NEWL})
 /* normal string/char seq character: any but quote,  or backslash */
STRCHAR           [^\'\"\\\n\a\b\v\t\f\r]

%%  /*############################### */

  /*----------------------  initial -----------------------*/

<INITIAL>{BLANK}+ {
   m_position.column += yyleng;
}

<INITIAL>{NEWL} {
   m_position.column = 1; ++m_position.row;
}

<INITIAL>"\"" {
   // start string mode
   m_lexeme = "";
   tok.filepos.position = m_position;
   ++tok.filepos.position.column;
   BEGIN(MODE_STRING);
}

<INITIAL>"'" {
   // start charseq mode
   m_lexeme = "";
   tok.filepos.position = m_position;
   ++m_position.column;
   BEGIN(MODE_CHARSEQ);
}

<INITIAL>"/*" {
     // start of multi line comment
      BEGIN(MODE_ML_COMMENT);
}

<INITIAL>"//" {
    // start of single line comment
      BEGIN(MODE_SL_COMMENT);
}

<INITIAL>"state" {
   return do_tokenID(tok,quan_lexer::STATE_);
}

<INITIAL>"jump" {
   return do_tokenID(tok,quan_lexer::JUMP_);
}

<INITIAL>"append" {
   return do_tokenID(tok,quan_lexer::APPEND_);
}

<INITIAL>"emit" {
   return do_tokenID(tok,quan_lexer::EMIT_);
}

<INITIAL>"pushfpos" {
   return do_tokenID(tok,quan_lexer::PUSHFPOS_);
}

<INITIAL>"popfpos" {
   return do_tokenID(tok,quan_lexer::POPFPOS_);
}

<INITIAL>"default" {
   return do_tokenID(tok,quan_lexer::DEFAULT_);
}

<INITIAL>{IDCHAR}({IDCHAR}|{DIGIT})* {
   return do_identifier(tok);
}

<INITIAL>".." {
   return do_tokenID(tok,quan_lexer::DOT_DOT_);
}

<INITIAL>"$"|"."|"="|","|"|"|"&"|"^"|";"|"{"|"}"|"?"|"*"|"+"|"("|")"|"!"|"["|"]" {
   return do_tokenID(tok);
}

   /* ------------------- string  ---------------------*/

<MODE_STRING>"\"" {
   // end of the string
   ++m_position.column;
   BEGIN(INITIAL);
   return do_string_literal(tok);
}

<MODE_STRING>"\'" {
   m_lexeme += yytext;
   m_position.column += yyleng;
}

<MODE_STRING>{NEWL} {
   do_error(tok,"raw return char in string literal");
   m_position.column = 1; ++m_position.row;
   m_lexeme = "";
   BEGIN(INITIAL);
   return tok.m_token_id = quan_lexer::error;
}

   /*--------------charseq -----------------------------*/

<MODE_CHARSEQ>"'" {
   ++m_position.column;
   BEGIN(INITIAL);
   return do_charseq(tok);
}

<MODE_CHARSEQ>"\"" {
   m_lexeme += yytext;
   m_position.column += yyleng;
}

<MODE_CHARSEQ,MODE_STRING>{NUMERICESCAPE} {
   // e.g \x(ff)\d(127)\B(101);
   int base = 0;
   switch(yytext[1]){
      case 'x':
         base= 16;
         break;
      case 'd':
         base = 10;
         break;
      case 'B':
         base = 2;
         break;
      default:
         //shouldnt get here
         break;
   }
   char* end = nullptr;
   long char_value  = strtol(yytext+3,&end,base);
   bool error = false;
   if ( (end == nullptr) || (*end != ')')){
       do_error(tok,"numeric escape too big for char");
       error = true;
   }else{
      if (char_value > 255){
        do_error(tok,"numeric escape too big for char");
        error = true;
      }
   }
   m_position.column += yyleng;
   if (!error){
      m_lexeme += static_cast<char>(char_value);
   }else{
     m_lexeme = "";
     return tok.m_token_id = quan_lexer::error;
   }
}

<MODE_CHARSEQ,MODE_STRING>{CHARESCAPE} {
   // [\'\"\\abfnrtv]
   switch (yytext[1]){
       case '\'':
         m_lexeme += '\'';
         break;
       case '"':
         m_lexeme += '"';
         break;
       case '\\':
         m_lexeme += '\\';
         break;
       case 'a':
         m_lexeme += '\a';
         break;
      case 'b':
         m_lexeme += '\b';
         break;
      case 'f':
         m_lexeme += '\f';
         break;
      case 'n':
         m_lexeme += '\n';
         break;
      case 'r':
         m_lexeme += '\r';
         break;
      case 't':
         m_lexeme += '\t';
         break;
      case 'v':
         m_lexeme += '\v';
         break;
      default:
        do_error(tok,"Unhandled escape sequence ,please report");
        m_position.column += yyleng;
        return tok.m_token_id = quan_lexer::error;
        break;
   }
   m_position.column += yyleng;
}

<MODE_CHARSEQ>{NEWL} {
   do_error(tok,"raw return char in character_set");
   m_position.column = 1; ++m_position.row;
   m_lexeme = "";
   BEGIN(INITIAL);
   return tok.m_token_id = quan_lexer::error;
}

   /*---- common string  and character sequence --- */

<MODE_CHARSEQ,MODE_STRING>"\\"{NOTNL} {
   do_error(tok,"unknown escape char");
   m_lexeme = "";
   m_position.column += yyleng;
   return tok.m_token_id = quan_lexer::error;
}

<MODE_STRING,MODE_CHARSEQ>{STRCHAR}+  {
   m_lexeme += yytext;
   m_position.column += yyleng;
}

   /*--------------comment (multiline)  -----------------------------*/

<MODE_ML_COMMENT>"*"+"/"  {
   BEGIN(INITIAL);
}

<MODE_ML_COMMENT>{NEWL}  {
   //update the file pos
   m_position.column = 1; ++m_position.row;
}

<MODE_ML_COMMENT>.  {
   /* eat anything else  */
}

   /*--------------comment (single line)  -----------------------------*/

<MODE_SL_COMMENT>{NEWL} {
   m_position.column = 1; ++m_position.row;
   BEGIN(INITIAL);
}

<MODE_SL_COMMENT>. {
   /* eat anything else */
}

   /*anything else */

<INITIAL,MODE_CHARSEQ,MODE_STRING>. {
   do_error(tok,"undefined character");
   m_lexeme = "";
   m_position.column += yyleng;
   return tok.m_token_id = quan_lexer::error;
}

%%
